---
title: 汇编语言（三）
seo_title: seo名称
toc: true
indent: true
top: false
comments: true
archive: true
cover: false
mathjax: false
pin: false
top_meta: false
bottom_meta: false
sidebar:
  - toc
tag:
  - 汇编语言
  - 《汇编语言》
categories: 编程语言
keywords: 文章关键词
updated: ''
img: /medias/featureimages/27.webp
date:
summary: 汇编语言
---

# 汇编语言（三）
## 汇编语言进阶
### 1.状态寄存器
#### 1.1引言
{%list%}
CPU内部的一种特殊的寄存器，其中每一位都用于记录特定的信息，主要介绍8086的标志寄存器
{%endlist%}
{%warning%}
每个指令是否影响标志寄存器，以及影响哪些部分，都是不同的
{%endwarning%}
>**相关指令**的**执行结果**
为CPU执行**相关指令**提供**行为依据**
控制CPU的相关**工作方式**

![8086的标志寄存器](/image/HB_24.png)
![debug中的8086标志寄存器](/image/HB_26.png)
#### 1.2标志
**①ZF标志**
>记录**相关指令**的计算结果是否为`0`，如果为`0`，则该位为`1`
{%list%}
主要是add之类的运算指令以及逻辑运算指令
{%endlist%}

**②PF标志**
>记录**相关指令**执行后，其结果`bit`位中`1`的个数是否为**偶数**，若为偶数，则为`1`

**③SF标志**
>记录**相关指令**执行后，其结果是否为**负**
{%warning%}
这个标志的前提是进行有符号运算
{%endwarning%}
>计算机常用**补码**描述**有符号数**，如`10000001B`可以看作是**无符号数**`129`，也可以看作**有符号数**`-127`

**④CF标志**
>记录了**无符号运算**时运算结果的**最高有效位**像**更高位**的**进位值/借位值**
{%list%}
CF的含义取决于其被什么指令设置，如果为加法指令，则表示进位值，如果为减法指令，则表示借位值
{%endlist%}
![有效位](/image/HB_25.png)

**⑤OF标志**
>记录**有符号运算**的结果是否发生了**溢出**

{%warning%}
注意，只要执行了对应的指令，相关标志位就会发生改变，无论是有符号运算还是无符号运算，只是某些标志位只在某种特定情况下有意义罢了
{%endwarning%}
>如执行`0F0H+88H`，`CF`会变为`1`，因为如果这是一个**无符号运算**，会产生**进位**，`OF`也会变为`1`，因为如果这是一个**有符号运算**，会发生**溢出**

#### 1.3相关指令
**①`adc`指令**
>**格式**：`adc ax,bx`
{%list%}
带进位加法指令,adc ax,bx相当于ax+bx+CF
{%endlist%}
{%right%}
可以利用adc指令实现更大数据的加法运算
{%endright%}
>计算`1EF0001000H+2010001EF0H`，将**最高16位**放在`ax`中，**次高16位**放在`bx`中，**低16位**放在`cx`中，更大的数据同理
```
mov ax,001EH
mov bx,0F000H
mov cx,1000H
add cx,1EF0H
adc bx,1000H
adc ax,0020H
```

**②`sbb`指令**
>**格式**：`sbb ax,bx`
{%list%}
带进位减法指令，sbb ax,bx相当于ax-bx-CF
{%endlist%}
{%right%}
同上，可以利用sbb实现更大数据的减法运算
{%endright%}

**③`cmp`指令**
>**格式**：`cmp ax,bx`
{%list%}
比较指令，功能相当于减法指令，但是不保存结果，只影响标志寄存器
{%endlist%}
{%right%}
可以通过标志寄存器状态判断参数大小关系
{%endright%}
>`ZF`：判断两者是否**相等**

>**无符号数**
`CF`：若为`1`，则`ax < bx`，反之则`ax ≥ bx`

>**有符号数**
`SF`：若为1，则逻辑上`ax < bx`，反之逻辑上`ax ≥ bx`
`OF`：判断是否溢出，若**溢出**，则真实大小关系和逻辑大小关系**相反**，反之则一致

**④基于比较结果的条件转移**
{%right%}
检测相关标志，从而转移到标号处，可以配合cmp指令使用，以下列举无符号数的相关指令
{%endright%}
>`je 标号`：**相等**则转移
{%list%}
ZF标志为1
{%endlist%}
>`jne 标号`：**不等**则转移
{%list%}
ZF标志为0
{%endlist%}
>`jb 标号`：**低于**则转移
{%list%}
CF标志为1
{%endlist%}
>`jnb 标号`：**不低于**则转移
{%list%}
CF标志为0
{%endlist%}
>`ja 标号`：**高于**则转移
{%list%}
CF标志为0且ZF标志为0
{%endlist%}
>`jna 标号`：**不高于**则转移
{%list%}
CF标志为1且ZF标志为1
{%endlist%}

**⑤串传送指令**
>**DF标志**：控制**串处理过程**中相关寄存器（`si`和`di`）**移动方向**，若为`0`，则**增加**，反之则减少

>**串传送指令**
`movsb`：将`ds:[si]`中数据移动到`es:[di]`，并根据DF标志移动`si`和`di`，每次操作**1个内存单位**
`movsw`：同上，但是每次操作**2个内存单位**

>**相关辅助指令**
`rep 串传送指令`：根据`cx`的值，**重复执行**其后的串传送指令
`cld`：将**DF标志**设为`0`
`std`：将**DF标志**设为`1`

**⑥访问标志寄存器**
>`pushf`：将**整个标志寄存器**的值压栈
`popf`：从栈中弹出数据，送入**标志寄存器**
***
### 2.中断
#### 2.1引言
**①定义**
>当CPU接收到**中断信息**后，**执行完**当前正在执行的指令后，**不再向下执行**，转去处理中断信息
{%warning%}
中断的优先级不一定是最高的，比如在修改栈顶地址过程中发生中断，他是不会响应中断的
{%endwarning%}
**②内中断**
{%list%}
以8086CPU为例
{%endlist%}
>**除法错误**，中断类型码为`0`
**单步执行**，中断类型码为`1`
{%list%}
当TF标志为1，则产生单步中断
{%endlist%}
>**执行`into`指令**，中断类型码为`4`
**执行`int`指令**，指令格式为`int n`，其中`n`为中断类型码
{%list%}
利用int指令主动引发中断
{%endlist%}
**③外中断**
>**可屏蔽外中断**：**可以不响应**的中断，
{%list%}
是否相应外中断由IF标志决定，可以通过指令设置其状态
{%endlist%}
>**不可屏蔽外中断**：执行完当前指令，**立即响应**

#### 2.2中断处理程序
**①中断向量表**
>提供**中断类型码**与其对应**中断处理程序入口地址**的关系，一个表项占**两个内存单元**，**高地址字**存放**段地址**，**低地址字**存放**偏移地址**
{%list%}
中断向量表保存在内存特定位置，对于8086CPU，放在内存0000:0000至0000:03FF
{%endlist%}
**②中断过程**
{%list%}
以8086CPU为例，由CPU自动执行
{%endlist%}
>从**中断信息**中获取**中断类型码**
**标志寄存器**的值**入栈**，并将标志寄存器的**TF标志**和**IF标志**设为`0`
`CS`和`IP`的值**入栈**
根据**终端类型码**将`CS:IP`指向**中断程序入口地址**

**③中断程序常规步骤**
>保存**用到的寄存器**`->`处理中断`->`**恢复**用到的寄存器`->`用`iret`指令返回
{%list%}
iret指令相当于pop IP、pop CS和popf
{%endlist%}

**④BIOS**
{%list%}
系统板的ROM中存放的程序（基本输入输出系统），主要包括对硬件系统的检测和初始化程序，以及各种中断例程
{%endlist%}
{%right%}
一个中断例程通常包含多个子程序，根据相关寄存器决定执行哪个子程序以及相关效果
{%endright%}
>如以下常用的**中断例程**，设置`ax`表示使用**程序返回**功能
```
       mov ax,4c00h
       int 21h
```
![BIOS安装过程](/image/HB_27.png)


#### 2.3自定义中断处理程序
{%list%}
以8086下除法溢出为例，编写程序安装自定义中断程序
{%endlist%}
```
assume cs:code

code segment
start: mov ax,cs
       mov ds,ax         ;设置ds:si指向中断程序源地址
       mov si,offset do0 

       mov ax,0
       mov es,ax         ;设置es:di指向中断程序最终目标地址
       mov di,200h

       mov cx,offset do0end-offset do0 ;设置串传输的长度

       cld ;设置传输方向
       rep movsb

       ;设置中断向量表
       mov ax,0
       mov es,ax
       mov word ptr es:[0*4],200h
       mov word ptr es:[0*4+2],0


       mov ax,4c00h
       int 21h
do0：  jmp short do0start
       db "overflow"

do0start: mov ax,cs
          mov ds,ax   ;将ds:si指向字符串
          mov si,202h

          mov ax,0b800h
          mov es,ax         ;设置es:di指向显存空间的中间位置
          mov di,12*160+36*2

          mov cx,9
s:        mov al,[si]
          mov es:[di],al
          inc si
          add di,2
          loop s

          mov ax 4c00h
          int 21h
do0end: nop

code ends
end start

```
**①申请空间**
>由于CPU**随时**都有可能收到**中断信息**，所以中断处理程序必须**一直存储在某段内存空间中**，所以需要申请一段**不会被别的程序使用**的空间
{%list%}
在这里为了简便，直接使用了0000:0200至0000:02FF这段空间，因为这段空间是8086中断向量表空余空间，不会被使用
{%endlist%}
**②安装**
>**安装中断处理程序**主要分为两步，即将**编写的程序复制到申请的空间中**，并将对应**中断向量表项设置为申请空间起始地址**

**③编写中断程序**
{%list%}
在中断程序中使用寄存器，需要先将其中内容保存字栈中
{%endlist%}
{%warning%}
本例中直接退出程序，如果中断过后还需要返回程序，则需要将mov ax 4c00h和int 21h替换为iret指令
{%endwarning%}
{%wrong%}
中断处理程序用到的数据(如overflow)需要在中断处理程序中申请，并记得设置程序的执行起点
{%endwrong%}

### 3.子程序
#### 3.1引言
**①带长度信息的标号**
>**标号**实质上就是**对应位置的起始地址**，一些标号**不带长度信息（如`a`）**，一些标号**带长度信息（如`b`）**，格式上表现为**后面**是否跟一个**冒号**`:`，以及对应**描述大小的伪指令**
```
assume ds:data
data segment
a: dw 1,2,3,4,5,6,7,8
b  dw 1,2,3,4,5,6,7,8
c  dw a,b
data ends
```
{%list%}
带有长度的标号相当于一个对应长度的内存单元
{%endlist%}
```
mov a,2 ;错误，没有指定长度信息
mov word ptr a,2
mov b,2 ;相当于mov word ptr cs:[16],2
```
**②`assume`详解**
>`assume`将**对应段寄存器**和**对应段**联系在一起，表现为位于**对应段**的**标号**自动取**对应段寄存器**中的内容作为**段地址**
{%list%}
以下三条指令是等价的，因为标号b在data段中，会默认取ds寄存器中内容为段地址
{%endlist%}
```
mov ax,b[bx+si+3]
mov ax,ds:0[bx+si+3]
mov ax,0[bx+si+3]
```
{%warning%}
若想使用标号访问数据，需要使用使用伪指令assume将标号所在段和一个段寄存器联系起来，否则编译器无法确定标号的段地址处于哪个寄存器中，但是并没有直接将对应寄存器的值修改为关联地址
{%endwarning%}
{%wrong%}
后面有:的地址标号，只能在代码段中使用
{%endwrong%}

#### 3.2直接定址表
{%list%}
一种映射关系，如0~15和十六进制的0~F，本质上是一种以空间换时间的做法
{%endlist%}
>以下为利用**直接定址表**求对应`sin`值的**子程序**

```
showsin: jmp short show
         ;存放各个标号的地址
         table dw ag0,ag30,ag60,ag90,ag120,ag150,ag180
         ;各个角度对应的sin值的字符串
         ag0  db '0',0
         ag30 db '0.5',0
         ag60 db '0.866',0
         ag90 db '1',0
         ag120 db '0.866',0
         ag150 db '0.5',0
         ag180 ad '0',0

show:    push bx
         push es
         push si
         mov bx,0b800h
         mov es,bx
         ;用ax传递的角度除以30作为相对于table的偏移量，取得对应字符串的偏移地址，放在bx中
         mov ah,0
         mov bl,30
         div bl
         mov bl,al
         mov bh,0
         add bx,bx
         mov bx,table[bx]

         ;显示对应字符串
         mov si,160*12+40*2
shows:   mov ah,cs:[bx]
         cmp ah,0
         je showret
         mov es:[si],ah
         inc bx
         add si,2
         jmp short shows

showret: pop si
         pop es
         pop bx
         ret
```

#### 3.3可选效果的程序
{%right%}
利用直接定址表实现程序不同功能
{%endright%}
>一个子程序`setscreen`，当`ah`为**不同的值**时，实现4种**不同的功能**
```
setscreen: jmp short set
           table dw sub1,sub2,sub3,sub4

set:       push bx

           cmp ah,3 ;判断异常情况，即是否超出了功能号范围
           ja sret
           mov bl,ah
           mov bh,0
           add bx,bx ;计算ah功能号对应位置

           call word ptr table[bx]

sret:      pop bx
           ret

;清空屏幕：将显存中当前屏幕中字符全部设为空格符
sub1:      push bx
           push cx
           push es

           mov bx,0b800h
           mov es,bx
           mov bx,0
           mov cx,2000

sub1s:     mov byte ptr es:[bx],' '
           add bx,2
           loop sub1s
           pop es
           pop cx
           pop bx
           ret

;设置前景色：将显存中当前屏幕处于奇地址属性字节的0、1、2位设置为al对应值
sub2:      push bx
           push cx
           push es

           mov bx,0b800h
           mov es,bx
           mov bx,1
           mov cx,2000

sub2s:     and byte ptr es:[bx],11111000b
           or es:[bx],al
           add bx,2
           loop sub2s

           pop es
           pop cx
           pop bx
           ret

;设置背景色：将显存中当前屏幕处于奇地址属性字节的4、5、6位设置为al对应值
 sub3:     push bx
           push cx
           push es

           mov cl,4
           shl al,cl
           mov bx,0b800h
           mov es,bx
           mov bx,1
           mov cx,2000

sub3s:     and byte ptr es:[bx],10001111b
           or es:[bx],al
           add bx,2
           loop sub2s

           pop es
           pop cx
           pop bx
           ret  
;向上滚动一行：依次将n+1行的内容复制到第n行，最后一行为空
sub4:      push cx
           push si
           push di
           push es
           push ds

           mov si,0b800h
           mov es,si
           mov ds,si
           mov si,160 ;将ds:si指向n+1行
           mov di,0 ;将es:di指向n行
           cld
           mov cx,24 ;共复制24行

sub4s:     push cx
           mov cx,160
           rep movsb  ;复制
           pop cx
           loop sub4s

           mov cx,80
           mov si,0

sub4s1:    mov byte ptr [160*24+si],' '
           add si,2
           loop sub4s1

           pop ds
           pop es
           pop di
           pop si
           pop cx
           ret
```


